# Mathew's XPP Implementation of the set of ODEs the equations David has derived as part of his model of electrocortical dynamics.
# This is now the implementation of the corrected set of equations.
#
# Mathew P Dafilis 1997
#
# Initial Implementation commenced Tuesday 10 June 1997.
#
# Equations modified to now include modifications for voltage dependent NMDA receptor function
# using a form of excitatory channel kinetics as outlined in Bojak and Liley (2005) and 
# parametrized according to Hestrin et al (1990) (as cited in Kandel, Schwartz and Jessel (2000) )
#
#
# User defined functions - enlin and inlin
#
t1e(x)=1+exp(-ve*(x-te))
enlin(x)=(emax*(1/(t1e(x))))
t1i(x)=1+exp(-vi*(x-ti))
inlin(x)=(imax*(1/(t1i(x))))
#
# The (corrected) ODEs
#
he'=(1/taue)*( (her-he) + (((heqee-he)*iee)/abs(heqee-her)) + (((heqie-he)*iie)/abs(heqie-her)))
hi'=(1/taui)*( (hir-hi) + (((heqei-hi)*iei)/abs(heqei-hir)) + (((heqii-hi)*iii)/abs(heqii-hir)))
iee'=ieeaux
ieeaux'=(1-cce)*gee*aee*exp(1)*(nbee*enlin(he) + pe*pee + phiee) - (gee^2)*iee - 2*gee*ieeaux
iie'=iieaux
iieaux'=(1-cci)*gie*aie*exp(1)*(nbie*inlin(hi) + pie) - (gie^2)*iie -2*gie*iieaux
iei'=ieiaux
ieiaux'=(1-cce)*gei*aei*exp(1)*(nbei*enlin(he) + pe*pei + phiei) - (gei^2)*iei - 2*gei*ieiaux
iii'=iiiaux
iiiaux'=(1-cci)*gii*aii*exp(1)*(nbii*inlin(hi) + pii) - (gii^2)*iii -2*gii*iiiaux
#
# Augment with time dependent equations for long range cortico-cortical fibres
#
phiee'=phieeaux
phieeaux'=naee*(lambdaee^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaee*phieeaux)-(vbar^2)*(lambdaee^2)*phiee
phiei'=phieiaux
phieiaux'=naei*(lambdaei^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaei*phieiaux)-(vbar^2)*(lambdaei^2)*phiei
#
# Auxilliary variables
aux efr=1000*enlin(he)
aux ifr=1000*inlin(hi)
#
# Parameters (all in millivolts, milliseconds, centimetres)
# (from column 3 table V Bojak and Liley (2005)
#
param cci=0
param cce=0
param pe=1.0
param her=-74.3857
param hir=-65.3258
param heqee=-16.7752
param heqei=-18.0371
param heqie=-83.2795
param heqii=-86.6430
param naee=2877.5554
param naei=2195.9506
param nbee=2839.9924
param nbei=4649.2592
param nbie=749.4045
param nbii=129.6441
param lambdaee=0.6707
param lambdaei=0.6707
param vbar=0.7477
param aee=0.2083
param aei=0.7203
param aie=0.4606
param aii=0.3748
param gee=0.4097
param gei=0.9327
param gie=0.3312
param gii=0.0808
param taue=77.7275
param taui=59.6486
param te=-46.7456
param ti=-42.1647
param rabs=0.0000
param emax=0.4628
param imax=0.2717
param pee=2.7363
param pei=3.7307
param pie=0.0000
param pii=0.0000
param se=6.3225
param si=3.3033
# some additional definitions
ve=sqrt(2)/se
vi=sqrt(2)/si
#
# Initial conditions
#
he(0)=-70
hi(0)=-70
iee(0)=0,ieeaux(0)=0
iie(0)=0,iieaux(0)=0
iei(0)=0,ieiaux(0)=0
iii(0)=0,iiiaux(0)=0
#
phiee(0)=0,phieeaux(0)=0
phiei(0)=0,phieiaux(0)=0
#
#
@ MAXSTOR=1000000
@ BACK=White
#
# Additional options - added Tuesday 19 August 1997
#
# Integrgee for 10 seconds (i.e 10000 milliseconds)
#
@ TOTAL=10000, DT=0.1, BOUND=1000000
@ XP=T, YP=HE
@ XLO=0, YLO=-100, XHI=10000, YHI=0
#
# AUTO parameters
@ NMAX=20000, PARMIN=0, PARMAX=1.0, NORMMIN=-1e8, NORMMAX=1e8
@ AUTOXMIN=0, AUTOXMAX=1.0, AUTOYMIN=-100, AUTOYMAX=-20
#
# Done.
done
