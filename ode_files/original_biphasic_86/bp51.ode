# Mathew's XPP Implementation of the set of ODEs the equations David has derived as part of his model of electrocortical dynamics.
# This is now the implementation of the corrected set of equations.
#
# Mathew P Dafilis 1997
#
# Initial Implementation commenced Tuesday 10 June 1997.
#
# Equations modified to now include modifications for voltage dependent NMDA receptor function
# using a form of excitatory channel kinetics as outlined in Bojak and Liley (2005) and 
# parametrized according to Hestrin et al (1990) (as cited in Kandel, Schwartz and Jessel (2000) )
#
#
# User defined functions - enlin and inlin
#
t1e(x)=1+exp(-ve*(x-te))
enlin(x)=(emax*(1/(t1e(x))))
t1i(x)=1+exp(-vi*(x-ti))
inlin(x)=(imax*(1/(t1i(x))))
#
# The (corrected) ODEs
#
he'=(1/taue)*( (her-he) + (((heqee-he)*iee)/abs(heqee-her)) + (((heqie-he)*iie)/abs(heqie-her)))
hi'=(1/taui)*( (hir-hi) + (((heqei-hi)*iei)/abs(heqei-hir)) + (((heqii-hi)*iii)/abs(heqii-hir)))
iee'=ieeaux
ieeaux'=(1-cce)*gee*aee*exp(1)*(nbee*enlin(he) + pe*pee + phiee) - (gee^2)*iee - 2*gee*ieeaux
iie'=iieaux
iieaux'=(1-cci)*gie*aie*exp(1)*(nbie*inlin(hi) + pie) - (gie^2)*iie -2*gie*iieaux
iei'=ieiaux
ieiaux'=(1-cce)*gei*aei*exp(1)*(nbei*enlin(he) + pe*pei + phiei) - (gei^2)*iei - 2*gei*ieiaux
iii'=iiiaux
iiiaux'=(1-cci)*gii*aii*exp(1)*(nbii*inlin(hi) + pii) - (gii^2)*iii -2*gii*iiiaux
#
# Augment with time dependent equations for long range cortico-cortical fibres
#
phiee'=phieeaux
phieeaux'=naee*(lambdaee^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaee*phieeaux)-(vbar^2)*(lambdaee^2)*phiee
phiei'=phieiaux
phieiaux'=naei*(lambdaei^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaei*phieiaux)-(vbar^2)*(lambdaei^2)*phiei
#
# Auxilliary variables
aux efr=1000*enlin(he)
aux ifr=1000*inlin(hi)
#
# Parameters (all in millivolts, milliseconds, centimetres)
# (from column 3 table V Bojak and Liley (2005)
#
param cci=0
param cce=0
param pe=1.0
param her=-74.1787
param hir=-62.1774
param heqee=-16.2564
param heqei=-8.6829
param heqie=-78.0061
param heqii=-79.4802
param naee=2149.5039
param naei=2448.7069
param nbee=2786.5639
param nbei=2802.0442
param nbie=551.5936
param nbii=288.9228
param lambdaee=0.9693
param lambdaei=0.9693
param vbar=0.7439
param aee=0.3274
param aei=0.4718
param aie=0.7794
param aii=0.9899
param gee=0.1254
param gei=0.6271
param gie=0.2105
param gii=0.1297
param taue=81.9611
param taui=143.3945
param te=-51.1291
param ti=-51.1088
param rabs=0.0000
param emax=0.2227
param imax=0.1223
param pee=4.8045
param pei=2.6428
param pie=0.0000
param pii=0.0000
param se=4.4783
param si=4.8342
# some additional definitions
ve=sqrt(2)/se
vi=sqrt(2)/si
#
# Initial conditions
#
he(0)=-70
hi(0)=-70
iee(0)=0,ieeaux(0)=0
iie(0)=0,iieaux(0)=0
iei(0)=0,ieiaux(0)=0
iii(0)=0,iiiaux(0)=0
#
phiee(0)=0,phieeaux(0)=0
phiei(0)=0,phieiaux(0)=0
#
#
@ MAXSTOR=1000000
@ BACK=White
#
# Additional options - added Tuesday 19 August 1997
#
# Integrgee for 10 seconds (i.e 10000 milliseconds)
#
@ TOTAL=10000, DT=0.1, BOUND=1000000
@ XP=T, YP=HE
@ XLO=0, YLO=-100, XHI=10000, YHI=0
#
# AUTO parameters
@ NMAX=20000, PARMIN=0, PARMAX=1.0, NORMMIN=-1e8, NORMMAX=1e8
@ AUTOXMIN=0, AUTOXMAX=1.0, AUTOYMIN=-100, AUTOYMAX=-20
#
# Done.
done
