# ODE file for XPPAUT
#
# User defined functions - enlin and inlin
#
t1e(x)=1+exp(-sqrt(2)*(x-te)/se)
enlin(x)=(emax*(1/(t1e(x))))
t1i(x)=1+exp(-sqrt(2)*(x-ti)/si)
inlin(x)=(imax*(1/(t1i(x))))
#
# The (corrected) ODEs
#
he'=(1/taue)*((her-he) + (((heqee-he)*iee)/abs(heqee-her)) + (((heqie-he)*iie)/abs(heqie-her)))
hi'=(1/taui)*((hir-hi) + (((heqei-hi)*iei)/abs(heqei-hir)) + (((heqii-hi)*iii)/abs(heqii-hir)))
iee'=ieeaux
ieeaux'=gee*aee*exp(1)*(nbee*enlin(he) + pee + phiee) - (gee^2)*iee - 2*gee*ieeaux
iie'=iieaux
iieaux'=R*gie*aie*exp(1)*(nbie*inlin(hi) + pie) - (gie^2)*iie -2*gie*iieaux
iei'=ieiaux
ieiaux'=gei*aei*exp(1)*(nbei*enlin(he) + pei + phiei) - (gei^2)*iei - 2*gei*ieiaux
iii'=iiiaux
iiiaux'=R*gii*aii*exp(1)*(k*nbii*inlin(hi) + pii) - (gii^2)*iii -2*gii*iiiaux
#
# Augment with time dependent equations for long range cortico-cortical fibres
#
phiee'=phieeaux
phieeaux'=naee*(lambdaee^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaee*phieeaux)-(vbar^2)*(lambdaee^2)*phiee
phiei'=phieiaux
phieiaux'=naei*(lambdaei^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaei*phieiaux)-(vbar^2)*(lambdaei^2)*phiei
#
# Parameters (all in millivolts, milliseconds, centimetres)
# (from column 3 table V Bojak and Liley (2005)
#
param R=1
param k=0.25
param her=-66.979487140
param hir=-60.349079520
param heqee=1.092379499
param heqei=-2.119864292
param heqie=-84.697721260
param heqii=-76.849916850
param naee=3615.593622000
param naei=1534.335503000
param nbee=4813.658093000
param nbei=3933.823826000
param nbie=596.608020800
param nbii=359.557035000
param lambdaee=0.145468446
param lambdaei=0.145468446
param vbar=0.755182
param aee=0.158722772
param aei=0.953624492
param aie=0.659990518
param aii=0.466926377
param gee=0.727771
param gei=0.639159
param gie=0.255589
param gii=0.0683505
param taue=125.538
param taui=119.21
param te=-54.351112470
param ti=-43.697692690
param rabs=0.0000
param emax=0.388074
param imax=0.441027
param pee=-200.0
param pei=200.0
param pie=0
param pii=0
param se=5.064414322
param si=4.917451720
#
# Initial conditions
#
he(0)=-70
hi(0)=-70
iee(0)=0,ieeaux(0)=0
iie(0)=0,iieaux(0)=0
iei(0)=0,ieiaux(0)=0
iii(0)=0,iiiaux(0)=0
#
phiee(0)=0,phieeaux(0)=0
phiei(0)=0,phieiaux(0)=0
#
#
@ MAXSTOR=1000000
@ BACK=White
#
# Integrate 
#
@ TOTAL=10000, DT=0.025, NOUT=200000, BOUND=1000000
@ XP=T, YP=HE
@ XLO=0, YLO=-100, XHI=10000, YHI=0
#
# AUTO parameters
@ NMAX=20000, PARMIN=0, PARMAX=1.0, NORMMIN=-1e8, NORMMAX=1e8
@ AUTOXMIN=0, AUTOXMAX=1.0, AUTOYMIN=-100, AUTOYMAX=-20
#
# Done.
done
