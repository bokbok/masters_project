# ODE file for XPPAUT
#
# User defined functions - enlin and inlin
#
t1e(x)=1+exp(-sqrt(2)*(x-te)/se)
enlin(x)=(emax*(1/(t1e(x))))
t1i(x)=1+exp(-sqrt(2)*(x-ti)/si)
inlin(x)=(imax*(1/(t1i(x))))
#
# The (corrected) ODEs
#
he'=(1/taue)*((her-he) + (((heqee-he)*iee)/abs(heqee-her)) + (((heqie-he)*iie)/abs(heqie-her)))
hi'=(1/taui)*((hir-hi) + (((heqei-hi)*iei)/abs(heqei-hir)) + (((heqii-hi)*iii)/abs(heqii-hir)))
iee'=ieeaux
ieeaux'=gee*aee*exp(1)*(nbee*enlin(he) + pee + phiee) - (gee^2)*iee - 2*gee*ieeaux
iie'=iieaux
iieaux'=R*gie*aie*exp(1)*(nbie*inlin(hi) + pie) - (gie^2)*iie -2*gie*iieaux
iei'=ieiaux
ieiaux'=gei*aei*exp(1)*(nbei*enlin(he) + pei + phiei) - (gei^2)*iei - 2*gei*ieiaux
iii'=iiiaux
iiiaux'=R*gii*aii*exp(1)*(k*nbii*inlin(hi) + pii) - (gii^2)*iii -2*gii*iiiaux
#
# Augment with time dependent equations for long range cortico-cortical fibres
#
phiee'=phieeaux
phieeaux'=naee*(lambdaee^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaee*phieeaux)-(vbar^2)*(lambdaee^2)*phiee
phiei'=phieiaux
phieiaux'=naei*(lambdaei^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaei*phieiaux)-(vbar^2)*(lambdaei^2)*phiei
#
# Parameters (all in millivolts, milliseconds, centimetres)
# (from column 3 table V Bojak and Liley (2005)
#
param R=1
param k=0.25
param her=-76.079314940
param hir=-74.404762400
param heqee=0.178294149
param heqei=-9.914806550
param heqie=-86.553761130
param heqii=-83.382532050
param naee=3456.048018000
param naei=2023.755319000
param nbee=4001.440558000
param nbei=3123.214953000
param nbie=653.869415700
param nbii=100.468333800
param lambdaee=0.435633322
param lambdaei=0.435633322
param vbar=0.116761
param aee=0.600101670
param aei=1.986702646
param aie=0.472283903
param aii=0.658230898
param gee=0.981054
param gei=0.185409
param gie=0.280966
param gii=0.0503701
param taue=82.9
param taui=111.665
param te=-50.804986380
param ti=-46.270516840
param rabs=0.0000
param emax=0.374328
param imax=0.333821
param pee=-200.0
param pei=200.0
param pie=0
param pii=0
param se=5.685754092
param si=4.833124529
#
# Initial conditions
#
he(0)=-70
hi(0)=-70
iee(0)=0,ieeaux(0)=0
iie(0)=0,iieaux(0)=0
iei(0)=0,ieiaux(0)=0
iii(0)=0,iiiaux(0)=0
#
phiee(0)=0,phieeaux(0)=0
phiei(0)=0,phieiaux(0)=0
#
#
@ MAXSTOR=1000000
@ BACK=White
#
# Integrate 
#
@ TOTAL=10000, DT=0.025, NOUT=200000, BOUND=1000000
@ XP=T, YP=HE
@ XLO=0, YLO=-100, XHI=10000, YHI=0
#
# AUTO parameters
@ NMAX=20000, PARMIN=0, PARMAX=1.0, NORMMIN=-1e8, NORMMAX=1e8
@ AUTOXMIN=0, AUTOXMAX=1.0, AUTOYMIN=-100, AUTOYMAX=-20
#
# Done.
done
