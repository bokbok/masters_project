# ODE file for XPPAUT
#
# User defined functions - enlin and inlin
#
t1e(x)=1+exp(-sqrt(2)*(x-te)/se)
enlin(x)=(emax*(1/(t1e(x))))
t1i(x)=1+exp(-sqrt(2)*(x-ti)/si)
inlin(x)=(imax*(1/(t1i(x))))
#
# The (corrected) ODEs
#
he'=(1/taue)*((her-he) + (((heqee-he)*iee)/abs(heqee-her)) + (((heqie-he)*iie)/abs(heqie-her)))
hi'=(1/taui)*((hir-hi) + (((heqei-hi)*iei)/abs(heqei-hir)) + (((heqii-hi)*iii)/abs(heqii-hir)))
iee'=ieeaux
ieeaux'=gee*aee*exp(1)*(nbee*enlin(he) + pee + phiee) - (gee^2)*iee - 2*gee*ieeaux
iie'=iieaux
iieaux'=R*gie*aie*exp(1)*(nbie*inlin(hi) + pie) - (gie^2)*iie -2*gie*iieaux
iei'=ieiaux
ieiaux'=gei*aei*exp(1)*(nbei*enlin(he) + pei + phiei) - (gei^2)*iei - 2*gei*ieiaux
iii'=iiiaux
iiiaux'=R*gii*aii*exp(1)*(k*nbii*inlin(hi) + pii) - (gii^2)*iii -2*gii*iiiaux
#
# Augment with time dependent equations for long range cortico-cortical fibres
#
phiee'=phieeaux
phieeaux'=naee*(lambdaee^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaee*phieeaux)-(vbar^2)*(lambdaee^2)*phiee
phiei'=phieiaux
phieiaux'=naei*(lambdaei^2)*(vbar^2)*enlin(he)-(2*vbar*lambdaei*phieiaux)-(vbar^2)*(lambdaei^2)*phiei
#
# Parameters (all in millivolts, milliseconds, centimetres)
# (from column 3 table V Bojak and Liley (2005)
#
param R=1
param k=0.25
param her=-61.158696600
param hir=-70.556453980
param heqee=2.338615964
param heqei=-0.771874958
param heqie=-85.362852530
param heqii=-83.258528170
param naee=2373.869386000
param naei=1017.020760000
param nbee=2683.793837000
param nbei=3394.693751000
param nbie=896.873887500
param nbii=617.615450200
param lambdaee=0.472446372
param lambdaei=0.472446372
param vbar=0.776769
param aee=1.127756887
param aei=1.178817919
param aie=1.748396964
param aii=1.133410559
param gee=0.849389
param gei=0.131143
param gie=0.122246
param gii=0.0526839
param taue=126.946
param taui=131.261
param te=-51.044359450
param ti=-46.910667800
param rabs=0.0000
param emax=0.123519
param imax=0.243971
param pee=-200.0
param pei=200.0
param pie=0
param pii=0
param se=4.179637017
param si=3.915105589
#
# Initial conditions
#
he(0)=-70
hi(0)=-70
iee(0)=0,ieeaux(0)=0
iie(0)=0,iieaux(0)=0
iei(0)=0,ieiaux(0)=0
iii(0)=0,iiiaux(0)=0
#
phiee(0)=0,phieeaux(0)=0
phiei(0)=0,phieiaux(0)=0
#
#
@ MAXSTOR=1000000
@ BACK=White
#
# Integrate 
#
@ TOTAL=10000, DT=0.025, NOUT=200000, BOUND=1000000
@ XP=T, YP=HE
@ XLO=0, YLO=-100, XHI=10000, YHI=0
#
# AUTO parameters
@ NMAX=20000, PARMIN=0, PARMAX=1.0, NORMMIN=-1e8, NORMMAX=1e8
@ AUTOXMIN=0, AUTOXMAX=1.0, AUTOYMIN=-100, AUTOYMAX=-20
#
# Done.
done
